plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

ext {
    lwjglVersion = '3.3.4'
    
    lwjglPlatforms = [
        "natives-windows",
        "natives-windows-arm64",
        "natives-linux",
        "natives-linux-arm64",
        "natives-macos",
        "natives-macos-arm64"
    ]
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"

    lwjglPlatforms.each { platform ->
        runtimeOnly "org.lwjgl:lwjgl::$platform"
        runtimeOnly "org.lwjgl:lwjgl-glfw::$platform"
        runtimeOnly "org.lwjgl:lwjgl-opengl::$platform"
        runtimeOnly "org.lwjgl:lwjgl-stb::$platform"
    }
}

application {
    mainClass = 'org.example.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

